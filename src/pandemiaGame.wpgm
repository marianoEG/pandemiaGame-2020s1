import wollok.game.*
import simulacion.*
import agenteSalud.*

object pandemiaGameConfig {
	method height() = 4
	method width() = 6
	method title() = "Simulador pandemia"
	
	method configureGame() {
		self.configureGameParameters()
		self.agregarManzanas()
		
		
		//PASAR DIA
		keyboard.p().onPressDo({ console.println("termino dia:") console.println( simulacion.diaActual()) simulacion.pasarDiaBarrio() })
		
		//IMPORTAR CASO
		keyboard.i().onPressDo({ simulacion.importarCaso() })
		
		//INFORME EN CONSOLA
		keyboard.c().onPressDo({ 
			console.println("++++++++++++++++++INFORME++++++++++++++++++")
			console.println("»dia:") console.println(simulacion.diaActual())
			console.println("»total de personas:") console.println(simulacion.cantidadDePersonasEnBarrio())
			console.println("»total de infectados:") console.println(simulacion.infectadosEnBarrio())
			console.println("»total de sintomaticos:") console.println(simulacion.cantidadSintomaticos())
			console.println("»total de aislados:") console.println(simulacion.cantidadAislados())
			console.println("»total de personas que respetan cuarentena:") console.println(simulacion.respetanLaCuarentena())
			console.println("+++++++++++++++++++++++++++++++++++++++++++")
		})
		
		//AGREGAR AGENTE DE SALUD (G)
		keyboard.g().onPressDo({if (not game.hasVisual(agenteSalud)){game.addVisual(agenteSalud)} else {game.removeVisual(agenteSalud)}})
		
		//MOVIMIENTO AGENTE DE SALUD
		keyboard.right().onPressDo({ if (agenteSalud.position().x() + 1 < self.width()){agenteSalud.moverDerecha()}})
		keyboard.left().onPressDo({ if (agenteSalud.position().x() > 0) {agenteSalud.moverIzquierda()}})
		keyboard.up().onPressDo({ if (agenteSalud.position().y() +1 < self.height()) {agenteSalud.moverArriba()}})
		keyboard.down().onPressDo({ if (agenteSalud.position().y() > 0) {agenteSalud.moverAbajo()}})
		
		//ACCIONES AGENTE DE SALUD
		keyboard.a().onPressDo({agenteSalud.aislarInfectadosConSintomas() game.say(agenteSalud, "Aislar infectados")})
		keyboard.f().onPressDo({agenteSalud.convencerQueRespetenCuarentena(game.uniqueCollider(agenteSalud)) game.say(agenteSalud, "Respeten la cuarentena por favor")})
		
		
	}
		
	method configureGameParameters() {
		game.height(self.height())
		game.width(self.width())
		game.title(self.title())
	}
	
	method agregarManzanas() {
		(1..game.width()).forEach({x => 
			(1..game.height()).forEach({y => 
				const nuevaManzana = simulacion.crearManzana()
				nuevaManzana.position(game.at(x-1,y-1))
				simulacion.agregarManzana(nuevaManzana)
				game.addVisual(nuevaManzana)
			})
		})
	}
	
}

program pandemiaGame {
	pandemiaGameConfig.configureGame()
	game.start()
}

